package InstantDummyCaster
import NoWurst
import Basics
import Unit
import Widget
import UnitObjEditing
import MapBounds
/*
 *   Dummy caster for casting spells
 *
 *   Spells must have 0 cooldown, 92083 range, and cost 0 mana. Spells must be instant and
 *   can't share the same order as other spells on the dummy caster.
 */
 
public class InstantDummyCaster
	static unit caster
	static int id
	
	private construct()
	
	private static function prepare(player owner, integer abilityId, integer lvl, vec2 pos)
		id = abilityId
		caster.addAbility(id)
		caster.setXY(pos)
		if lvl > 1
			caster.setAbilityLevel(id, lvl)
		if owner != null
			caster.setOwner(owner, false)
			
	private static function finish()
		caster.setOwner(DUMMY_PLAYER, false)
		caster.removeAbility(id)
		caster.setPos(boundMax)
			
	static function castImmediate(player owner, int abilityId, int lvl, int orderId, vec2 pos) returns boolean
		prepare(owner, abilityId, lvl, pos)
		boolean b = caster.issueImmediateOrderById(orderId)
		finish()
		return b
		
	static function castTarget(player owner, int abilityId, int lvl, int orderId, widget target, vec2 casterPos) returns boolean
		prepare(owner, abilityId, lvl, casterPos)
		caster.setFacing(caster.getPos().angleTo(target.getPos()))
		boolean b = caster.issueTargetOrderById(orderId, target)
		finish()
		return b
	
	static function castPoint(player owner, int abilityId, int lvl, int orderId, vec2 targetPos, vec2 casterPos) returns boolean
		prepare(owner, abilityId, lvl, casterPos)
		caster.setFacing(caster.getPos().angleTo(targetPos))
		boolean b = caster.issuePointOrderById(orderId, targetPos)
		finish()
		return b

	// shorters
	static function castTarget(player owner, int abilityId, int lvl, int orderId, widget target) returns boolean
		return castTarget(owner, abilityId, lvl, orderId, target, vec2(GetWidgetX(target), GetWidgetY(target)))
	static function castPoint(player owner, int abilityId, int lvl, int orderId, vec2 targetPos) returns boolean
		return castPoint(owner, abilityId, lvl, orderId, targetPos, targetPos)

	// string order wrappers
	static function castImmediate(player owner, int abilityId, int lvl, string order, vec2 pos) returns boolean
		return castImmediate(owner, abilityId, lvl, OrderId(order), pos)
	static function castTarget(player owner, int abilityId, int lvl, string order, widget target, vec2 casterPos) returns boolean
		return castTarget(owner, abilityId, lvl, OrderId(order), target, casterPos)
	static function castTarget(player owner, int abilityId, int lvl, string order, widget target) returns boolean
		return castTarget(owner, abilityId, lvl, OrderId(order), target)
	static function castPoint(player owner, int abilityId, int lvl, string order, vec2 targetPos, vec2 casterPos) returns boolean
		return castPoint(owner, abilityId, lvl, OrderId(order), targetPos, casterPos)
	static function castPoint(player owner, int abilityId, int lvl, string order, vec2 targetPos) returns boolean
		return castPoint(owner, abilityId, lvl, OrderId(order), targetPos)
		
constant DUMMY_CASTER_UNIT_ID = 'xcas'

init
	InstantDummyCaster.caster = createUnit(DUMMY_PLAYER, DUMMY_CASTER_UNIT_ID, vec2(0,0), angle(0))
	InstantDummyCaster.caster.setPos(boundMax)
		
@compiletime function generateObject()
		let u = new BuildingDefinition(DUMMY_CASTER_UNIT_ID, 'nfr2')
		u..setName("Dummy Caster")
		..setUpgradesUsed("")
		..setManaRegeneration(0.1)
		..setManaInitialAmount(1000)
		..setManaMaximum(1000)
		..setCollisionSize(0.0)
		..setRace(Race.Commoner)
		..setFoodCost(0)
		..setArmorType(ArmorType.Divine)
		..setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNtemp.blp")
		..setSpeedBase(522)
		..setModelFile("dummy.mdl")
		..setAnimationBlendTimeseconds(0.0)
		..setAnimationCastBackswing(0.0)
		..setMaximumPitchAngledegrees(0.0)
		..setMaximumRollAngledegrees(0.0)
		..setTurnRate(3)
		..setSpecial("")
		..setProjectileImpactZ(0.0)
		..setProjectileLaunchZ(0.0)
		..setNameEditorSuffix("(Basics)")
		..setSightRadiusDay(1)
		..setSightRadiusNight(1)
		..setUnitClassification("_")
		..setPropulsionWindowdegrees(1.0)
		..setTooltipBasic("")
		..setNormalAbilities("Aloc,Agho")
		..setGroundTexture("")
		..setShadowTextureBuilding("")
		..setCollisionSize(0)
		..setPathingMap("")
		..setNeutralBuildingShowsMinimapIcon(false)
