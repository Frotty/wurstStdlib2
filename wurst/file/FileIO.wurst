package FileIO
import public ChunkedString
import AbilityIds
import ErrorHandling

/* 	FileIO for Patches 1.31 and up with chunked string support for big data.
	Reads and writes using preload files by manipulating default ability tooltips.

	Read a file:
	>	let fileContent = new File("MyFileName.pld").readAsString()

	Write a file:
	>	new File("MyFileName.pld")..write("MyContent")..close()
*/

/** Abilities which tooltips will be used to load data */
@configurable constant IO_ABILITIES = [AbilityIds.aerialShackles, AbilityIds.rocketAttack, AbilityIds.callToArms,
						AbilityIds.calltoArms, AbilityIds.cloudofFog, AbilityIds.controlMagic,
						AbilityIds.defend, AbilityIds.dispelMagic, AbilityIds.feedbackArcaneTower,
						AbilityIds.feedback]

public var CAN_READ_FILES = true

public class File
	var filename = ""

	construct(string filename)
		this.filename = filename

	function write(string content)
		let buffer = new ChunkedString()
		buffer..append(content)..flush()
		write(buffer)

	function write(ChunkedString buffer)
		for i = 0 to buffer.chunkCount
			let str = buffer.getChunk(i)
			if validateInput(str) != null
				error("FileIO(" + filename + ") ERROR: Invalid character |cffffcc00" + validateInput(str) + "|r")

		writePreload(buffer)

	function close()
		destroy this

	function read() returns ChunkedString
		return readPreload()

	function readAsString() returns string
		let read = readPreload()
		return read != null ? read.getChunk(0) : "-"

	private function writePreload(ChunkedString buffer)
		// Begin to generate the file
		PreloadGenClear()
		PreloadGenStart()

		for i = 0 to buffer.chunkCount - 1
			if (i >= IO_ABILITIES.length)
				error("FileIO(" + filename + ") ERROR: String exceeds max length (" + I2S(IO_ABILITIES.length * buffer.chunkSize) + ").|r")

			let chunk = buffer.getChunk(i)
			Preload("\" )\ncall BlzSetAbilityTooltip(" + IO_ABILITIES[i].toString() + ", \"" + chunk + "\", 0)\n//")

		Preload("\" )\nendfunction\nfunction a takes nothing returns nothing\n //")
		PreloadGenEnd(this.filename)

	private function readPreload() returns ChunkedString
		if not CAN_READ_FILES
			Log.warn("Reading files is not supported on this system.")
			return null

		string array original
		let output = new ChunkedString(DEFAULT_CHUNK_SIZE)

		for i = 0 to IO_ABILITIES.length - 1
			original[i] = BlzGetAbilityTooltip(IO_ABILITIES[i], 0)

		// Execute the preload file
		Preloader(this.filename)

		// Read the output
		for i = 0 to IO_ABILITIES.length - 1
			// Make sure the tooltip has changed
			var chunk = BlzGetAbilityTooltip(IO_ABILITIES[i], 0)

			if chunk == original[i]
				break

			// Restore the tooltip and append the chunk
			BlzSetAbilityTooltip(IO_ABILITIES[i], original[i], 0)

			output.append(chunk)

		output.flush()

		return output

	private static function validateInput(string content) returns string
		for char in content
			if (char == "\\")
				return char
			else if (char == "\"")
				return char
		return null

init
	CAN_READ_FILES = new File("FileTester.pld")..write("test").readAsString() == "test"
