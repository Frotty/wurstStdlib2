package LinkedList
import NoWurst
import Wurstunit

public class LinkedList<T>
	private LLEntry<T> dummy = new LLEntry<T>(null, null, null)
	private LLStaticIterator<T> staticItr = new LLStaticIterator<T>(this)
	protected int size = 0

	/** Create a new empty list */
	construct()
		dummy.next = dummy
		dummy.prev = dummy

	/** Add an element to the end of the list (top of stack, beginning of queue) */
	function add(T elem)
		let entry = new LLEntry<T>(elem, dummy.prev, dummy)
		dummy.prev.next = entry
		dummy.prev = entry
		size++

	/** add all elements from elems to the end of this list */
	function addAll(LinkedList<T> elems)
		for elem in elems
			add(elem)
			
	/** Returns the element at the specified index */
	function get(int index) returns T
		return getEntry(index).elem

	/** Returns the index of the specified element or -1 is it doesn't exist */
	function indexOf(T t) returns int
		var entry = dummy.next
		int idx = 0
		while entry != dummy
			if entry.elem == t
				return idx
			entry = entry.next
			idx++
		return -1

	/** Sets the element at the specified index */
	function set(int index, T elem)
		getEntry(index).elem = elem
		
	/** Add an element to the end of the list (top of stack, beginning of queue) */
	function push(T elem)
		add(elem)

	/** Returns the first element in the list */
	function getFirst() returns T
		return dummy.next.elem

	/** Returns and removes the first added Element (FIFO) */
	function dequeue() returns T
		let top = dummy.next
		let elem = top.elem
		removeEntry(top)
		return elem
		
	/** Returns and removes the last added Element (LIFO) */
	function pop() returns T
		let top = dummy.prev
		let elem = top.elem
		removeEntry(top)
		return elem
		
	/** Returns and removes the lastly added Element */
	function peek() returns T
		return dummy.prev.elem
	
	/** Returns whether the lists contains the specified element */
	function contains(T elem) returns boolean
		var entry = dummy.next
		while entry != dummy
			if entry.elem == elem
				return true
			entry = entry.next
		return false
		
	/** Removes the element and it's entry at the given index */
	function removeAt(int index) returns T
		let entry = getEntry(index)
		entry.prev.next = entry.next
		entry.next.prev = entry.prev
		destroy entry	
		size--
		return entry.elem
		
	/** Removes the first occurence of t from this list */
	function remove(T elem)
		LLEntry<T> entry = dummy.next
		while entry != dummy
			if entry.elem == elem
				removeEntry(entry)
				return
			entry = entry.next
	
	/** gets the size of the list */
	function getSize() returns int
		return size
		
	/** get an iterator for this list */
	function staticItr() returns LLStaticIterator<T>
		staticItr.reset()
		return staticItr

	/** get an iterator for this list */
	function iterator() returns LLIterator<T>
		return new LLIterator(this)
	
	/** get a backiterator for this list */
	function backiterator() returns ListBackIterator<T>
		return new ListBackIterator(dummy)	
		
	/** adds an element to the beginning of the list */	
	function addtoStart(T elem)
		let entry = new LLEntry<T>(elem, dummy, dummy.next)
		dummy.next.prev = entry
		dummy.next = entry
		size++
		
	/** replaces the first occurence of 'whichElement' with 'newElement' 
		returns true when an element has been replaced, 
		false if 'whichelement' is not contained in the list
	*/
	function replace(T whichElement, T newElement) returns boolean
		LLEntry<T> current = dummy.next
		while current != dummy
			if current.elem == whichElement
				current.elem = newElement
				return true
			current = current.next
		return false

	/** Removes the element if the predicates returns true */
	function removeWhen(LinkedListPredicate<T> predicate) 
		LLEntry<T> r = dummy.next
		while r != dummy
			if predicate.isTrueFor(r.elem)
				r.prev.next = r.next
				r.next.prev = r.prev
				destroy r
				size--
			r = r.next
		destroy predicate
		
	/** Executes the closure for each element */	
	function forEach(LLItrClosure<T> itr)
		LLEntry<T> r = dummy.next
		while r != dummy
			itr.run(r.elem)
			r = r.next
		destroy itr
	
	/** Updates all elements */	
	function updateAll(LinkedListUpdater<T> f)
		LLEntry<T> r = dummy.next
		while r != dummy
			r.elem = f.update(r.elem) 
			r = r.next
		destroy f

	/** Pseudo-randomly changes the position of each element */
	function shuffle()
		for i = 0 to size
			add(removeAt(GetRandomInt(0, size-1)))
	
	/** Prints the content of the list */
	function toString() returns string
		string s="["
		var first = true
		for e in this
			if not first
				s += ", "
			s += I2S(e castTo int)
			first = false
		return s + "]"
	
	/** Removes all elements from the list */
	function clear()
		LLEntry<T> current = dummy.next
		while current != dummy
			current = current.next
			destroy current.prev
		dummy.next = dummy
		dummy.prev = dummy
		size = 0
		
	protected function getDummy() returns LLEntry<T>
		return dummy
		
	/** Returns the entry at the given index */
	private function getEntry(int index) returns LLEntry<T>
		var entry = dummy
		for int i = 0 to index
			entry = entry.next
		return entry
		
	/** Removes an entry */
	private function removeEntry(LLEntry<T> entry)
		entry.prev.next = entry.next
		entry.next.prev = entry.prev
		destroy entry
		size--
		
	ondestroy
		destroy staticItr
		LLEntry<T> current = dummy.next
		while current != dummy
			current = current.next
			destroy current.prev
		destroy dummy

class LLEntry<T>
	T elem
	LLEntry<T> prev
	LLEntry<T> next
	
	construct(T elem, LLEntry<T> prev, LLEntry<T> next)
		this.elem = elem
		this.prev = prev
		this.next = next

public class LLStaticIterator<T>
	LLEntry<T>    dummy
	LLEntry<T>    current
	LinkedList<T> parent

	construct(LinkedList<T> parent)
		this.parent  = parent
		reset()

	/** Returns the iterator back to it#s initial state */
	function reset()
		this.dummy   = parent.getDummy()
		this.current = dummy

	/** Removes from the list the last element that was returned by next() (optional operation).
	 	This call can only be made once per call to next */
	function remove() returns T
		if current != dummy
			parent.size--
			current.prev.next = current.next
			current.next.prev = current.prev
			let removedNode   = current
			current           = current.prev
			destroy removedNode
			return removedNode.elem
		return null

	function hasNext() returns boolean
		return current.next != dummy

	function next() returns T
		current = current.next
		return current.elem

	function lookahead() returns T
		if hasNext()
			return current.next.elem
		return null

	function close()
	

public class LLIterator<T>
	LLEntry<T>    dummy
	LLEntry<T>    current
	LinkedList<T> parent

	construct(LinkedList<T> parent)
		this.parent  = parent
		reset()

	function reset()
		this.dummy   = parent.getDummy()
		this.current = dummy

	/** Removes from the list the last element that was returned by next() (optional operation).
	 	This call can only be made once per call to next */
	function remove() returns T
		if current != dummy
			parent.size--
			current.next.prev = current.prev
			current.prev.next = current.next
			let removedNode   = current
			current           = current.prev
			destroy removedNode
			return removedNode.elem
		return null

	function hasNext() returns boolean
		return current.next != dummy

	function next() returns T
		current = current.next
		return current.elem

	function close()
		destroy this
		
public class ListBackIterator<T>
	LLEntry<T> dummy
	LLEntry<T> current

	construct(LLEntry<T> dummy)
		this.dummy = dummy
		this.current = dummy
			
	function hasNext() returns boolean
		return current.prev != dummy

	function next() returns T
		current = current.prev 
		return current.elem 
 		
	function close() 
		destroy this

public interface LinkedListPredicate<T>
	function isTrueFor(T t) returns boolean
	
public interface LLItrClosure<T>
	function run(T t)

public interface LinkedListUpdater<T>
	function update(T t) returns T

// =====TESTS=====

@test public function testAddRemove()
	let list = new LinkedList<int>()
	assertTrue(list.getSize() == 0)
	
	list..add(1)..add(2)..add(3)..add(4)
	assertTrue(list.getSize() == 4)
	
	let itr = list.staticItr()
	while itr.hasNext()
		itr.next()
		itr.remove()

	assertTrue(list.getSize() == 0)

@test public function testForEach()
	let list = new LinkedList<int>()
	
	list..add(1)..add(2)..add(3)..add(4)
	
	list.updateAll((int i) -> i + 1)
	
	var result = 0
	for i in list
		result += i

	assertTrue(result == (5+4+3+2))

@test public function testAddAll()
	let list = new LinkedList<int>()
	let list2 = new LinkedList<int>()
	
	list..add(1)..add(2)
	list2..add(3)..add(4)
	list.addAll(list2)

	var result = 0
	for i in list
		result += i

	assertTrue(list.get(2) == 3)
	assertTrue(list.get(3) == 4)
