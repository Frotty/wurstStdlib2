package DialogBox
import HashMap
import Dialog
import LinkedList

/**DialogBox lets you to create dialogs and associate the buttons with code.*/
public class DialogBox

    private static constant HashMap<button, trigger> buttonTriggers = new HashMap<button, trigger>
    private static constant HashMap<dialog, DialogBox> dialogBoxes = new HashMap<dialog, DialogBox>
    private constant LinkedList<button> buttons = new LinkedList<button>

    private constant dialog dialogHandle = createDialog()
    private constant trigger clickDialog = CreateTrigger()
        ..registerDialogEvent(this.dialogHandle)
        ..addAction(function removeClickedDialog)

    private string title
    

    construct(string title)
        this.title = title
        this.dialogHandle.setMessage(title)
        dialogBoxes.put(this.dialogHandle, this)

    private static function removeClickedDialog()
        let btn = GetClickedButton()
        buttonTriggers.get(btn).execute()
        destroy dialogBoxes.get(GetClickedDialog())

    function addButton(string buttonText, code cb)
        this.addButton(buttonText, 0 , cb)

    /**Hotkey: use ASCII number of the capital letter.*/
    function addButton(string buttonText, integer hotkey, code cb)
        let btn = this.dialogHandle.addButton(buttonText, hotkey)
        this.buttons.add(btn)
        let trg = CreateTrigger()
            ..addAction(cb)
        buttonTriggers.put(btn, trg)

    /**Toggles visibility of the DialogBox for a player. Dialogs are invisible by default
    Dialogs cannot be shown at map initialization*/
    function display(player whichPlayer, boolean flag)
        this.dialogHandle.display(whichPlayer, flag)

    function setTitle(string title)
        this.dialogHandle.setMessage(title)
        this.title = title

    function getTitle() returns string
        return this.title

    ondestroy
        buttons.forEach( (button btn) -> begin
            buttonTriggers.get(btn)
            ..clearActions()
            ..destr()
        end )

        destroy buttons
        dialogBoxes.remove(this.dialogHandle)
        this.dialogHandle.clear()
        this.dialogHandle.destr()
        this.clickDialog.clearActions()
        this.clickDialog.destr()
        
